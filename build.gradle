buildscript {
  repositories {
    jcenter()
    mavenCentral()
  }
}

plugins {
  id "com.github.hierynomus.license" version "0.14.0"
  // id "net.vivin.gradle-semantic-build-versioning" version "4.0.0"
  id "io.codearte.nexus-staging" version "0.11.0"
}

group = 'tech.hillview'
version = '0.2.0'
// version = ('git describe --tags'.execute()).text.trim()

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
  mavenCentral()
  jcenter()
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

defaultTasks 'jar'

jar {
  manifest {
    attributes(
      "Implementation-Title": project.name,
      "Implementation-Version": project.version
    )
  }
}

// configurations.all {
//   exclude group: "org.slf4j", module: "slf4j-log4j12"
//   exclude group: "log4j", module: "log4j"
// }

configurations {
  all*.exclude group: 'log4j', module: 'log4j'
  // all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
}

dependencies {
//  compile('org.apache.zookeeper:zookeeper:3.4.9') {
//    exclude group: 'org.slf4j', module: 'slf4j-log4j12'
//  }

  compile 'org.apache.curator:curator-x-discovery:4.0.1'

//  compile 'com.google.guava:guava:17.0'

  compile 'com.squareup.retrofit2:retrofit:2.4.0'
  // compile 'com.fasterxml.jackson.core:jackson-databind:2.9.5'
  compile 'com.squareup.retrofit2:converter-jackson:2.4.0'

  compile 'org.slf4j:slf4j-api:1.7.21'

  compileOnly 'org.springframework:spring-context:4.2.8.RELEASE'
  compileOnly 'org.springframework:spring-beans:4.2.8.RELEASE'

  testCompile 'ch.qos.logback:logback-core:1.1.7'
  testCompile 'ch.qos.logback:logback-classic:1.1.7'
  testCompile 'org.slf4j:jcl-over-slf4j:1.7.21'

  testCompile 'junit:junit:4.12'
  testCompile 'org.assertj:assertj-core:3.5.2'

  testCompile 'org.springframework:spring-test:4.2.8.RELEASE'
  testCompile 'org.springframework:spring-context:4.2.8.RELEASE'
  testCompile 'org.springframework:spring-beans:4.2.8.RELEASE'

  testCompile 'org.apache.curator:curator-test:2.11.0'
  testCompile 'com.sparkjava:spark-core:2.5.2'
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar
}

signing {
  sign configurations.archives
}

nexusStaging {
  username ossrhUsername
  password ossrhPassword
  stagingProfileId '2b040f5b1d1415'
  // packageGroup group
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment)  }
      /**
      * Download snapshot and release artifacts from group https://oss.sonatype.org/content/groups/public
      * Download snapshot, release and staged artifacts from staging group https://oss.sonatype.org/content/groups/staging
      */
      // repository(url: "file:///Users/tommy/myrepo/")
      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name "${project.group}:${project.name}"
        description 'Curator-Service-Api-Client is an API client factory for services registered in ZooKeeper via Curator Framework or Spring Cloud ZooKeeper, featuring Service Discovery and Load balancing.'
        url 'https://github.com/tommyxu/curator-service-api-client.git'
        scm {
          url 'https://github.com/tommyxu/curator-service-api-client.git'
          connection 'scm:git@github.com:tommyxu/curator-service-api-client.git'
        }
        licenses {
          license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
          }
        }
        organization {
          name 'github'
          url 'https://github.com/tommyxu'
        }
        developers {
          developer {
            id 'tommyxu'
            name 'Tommy Xu'
            email "xuyijun@gmail.com"
          }
        }
      }
    }
  }
}

test {
  testLogging {
    showStandardStreams = true
  }
}

license {
  header rootProject.file('LICENSE_HEADER')
  strictCheck true
}

